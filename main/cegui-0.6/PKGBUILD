# Maintainer : Lucio zara <pennega@gmail.com>


pkgname=cegui-0.6
pkgver=0.6.2
_pkgver=0.6.2b
pkgrel=13

pkgdesc="A free library providing windowing and widgets for graphics APIs/engines."
arch=('i686' 'x86_64')
url="http://www.cegui.org.uk/"
license=('MIT')
depends=('devil' 'freetype2' 'libxml2' 'lua' 'pcre'  'silly' 'glew' 'tinyxml' 'directfb')
makedepends=()
options=('!libtool')
conflicts=('cegui')
source=("http://downloads.sourceforge.net/crayzedsgui/CEGUI-${_pkgver}.tar.gz")
md5sums=('4fbd95e5a2ac1c7acf2a8f5df3ac6b93')
	

build() {
  cd ${srcdir}/CEGUI-${pkgver}
  #PATCH
  #sed -i '1i#include <cstddef>' include/CEGUIString.h
  sed -i '/#include <stdexcept>/a\#include <cstddef>' include/CEGUIString.h
  sed -i '/#include "CEGUIDynamicModule.h"/a\#include <algorithm>' RendererModules/directfbRenderer/directfb-renderer.cpp
  sed -i 's:TiXmlNode\:\::TiXmlNode\:\:TINYXML_:g' XMLParserModules/TinyXMLParser/CEGUITinyXMLParser.cpp
  sed -i 's:ILvoid:void:g' ImageCodecModules/DevILImageCodec/CEGUIDevILImageCodec.cpp

  PYTHON=python2 \
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var \
	      --disable-static \
	      --disable-xerces-c \
	      --disable-samples \
	      --enable-devil \
	      --enable-silly \
	      --enable-lua-module \
	      --enable-python-module \
	      --with-default-image-codec=SILLYImageCodec

  make || return 1
}

package() {
  cd ${srcdir}/CEGUI-${pkgver}
  install -d ${pkgdir}/usr/share/licenses/${pkgname} || return 1

  make DESTDIR=${pkgdir} install || return 1

  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1
}

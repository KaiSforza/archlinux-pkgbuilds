# Contributor: Sam S. <smls75@gmail.com>
# Contributor: Ben R. <thebenj88 *AT* gmail *DOT* com>

pkgname=eufloria-hib
pkgver=1.15_20121108
_hibver=2012-11-08
_gamepkgver=1.0.1
pkgrel=0
pkgdesc='A unique real time strategy game (Humble Bundle version)'
url='http://www.eufloria-game.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libgl' 'mesa' 'sdl' 'sdl_mixer' 'sdl_image')
optdepends=('catalyst: AMD Proprietary 3D driver'
			'nvidia: NVIDIA Proprietary 3D driver')
_gamepkg="EufloriaHD-Linux-${_gamepkgver}.sh"
source=('eufloria-hib.desktop'
        "$_gamepkg")
sha256sums=('637790aa33b43d2d2ef3178c07ff882f7c5f85f0b89ad46b971ceed5133c8783'
            '40271b57827d55f70818f85d8c0c9e1091cf8726119e3a99a59d07630f72a8fb')
options=("!upx")

package() {
  cd $srcdir

  # Execute installer
  msg "Starting setup..."
  [[ -d "${srcdir}/temp" ]] && rm -r "${srcdir}/temp"
  sh "${srcdir}/${_gamepkg}" --unattended --no-register --keep --overwrite \
                     --target  "${srcdir}/temp" \
                     --bindir  "${srcdir}/bin" \
                     --datadir "${pkgdir}/opt"

  # Remove bundled libraries & helper binaries (use system packages instead)
  rm -rf "${pkgdir}/opt/EufloriaHD/xdg-"*

  # Fix permissions
  find "${pkgdir}" -type f -exec chmod 644 "{}" +

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}/opt/EufloriaHD/EufloriaHD.png" \
                 "${pkgdir}/usr/share/pixmaps/eufloria.png"

  # Install launcher symlink
  case $CARCH in
      i686) _arch=x86;;
      x86_64) _arch=x86_64;;
  esac
  chmod 755 "${pkgdir}/opt/EufloriaHD/EufloriaHD.bin.${_arch}"
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/EufloriaHD/EufloriaHD.bin.${_arch}" "${pkgdir}/usr/bin/eufloria"

  # Install license file
  install -Dm644 "temp/config/license" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# $Id: PKGBUILD 79958 2012-11-15 03:14:52Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
pkgname=steam
pkgver=1.0.0.22
pkgrel=2
pkgdesc="Digital distribution client - open beta - bootstrap package"
arch=("i686" "x86_64")
url="http://steampowered.com/"
license=('custom')
depends=(
      'bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
      'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-liberation' 'zenity'
      )
if [[ "$CARCH" == "x86_64" ]]; then
  depends+=(
  'lib32-sdl' 'lib32-libvorbis' 'lib32-alsa-lib'
  'lib32-libgcrypt' 'lib32-gtk2' 'lib32-nss' 'lib32-openal'
  'lib32-libpng12' 'lib32-libpulse' 'lib32-libcanberra'
  'lib32-glu'
  )
  optdepends=(
  'lib32-ati-dri: for open source ATI driver users'
  'lib32-catalyst-utils: for AMD Catalyst users'
  'lib32-intel-dri: for open source Intel driver users'
  'lib32-nouveau-dri: for Nouveau users'
  'lib32-nvidia-utils-bumblebee: for NVIDIA + Bumblebee users'
  'lib32-nvidia-utils: for NVIDIA proprietary blob users'
  'lib32-libappindicator: for tray icon'
  'lib32-flashplugin: for flash video, copy to $XDG_DATA_HOME/Steam/ubuntu12_32/plugins/'
  )
else
  depends+=(
  'glu' 'libpng12' 'sdl' 'libpulse' 'alsa-lib' 'libvorbis'
  'libgcrypt' 'nss' 'openal' 'gtk2' 'glu'
  )
  optdepends=(
    'libappindicator: for tray icon'
  )
fi
install=steam.install

source=("$pkgname-$pkgver.deb::http://media.steampowered.com/client/installer/steam.deb")
md5sums=('46c4f770439abdf148317b3208ab035c')

build() {
  cd "$srcdir"

  bsdtar xf "$srcdir/data.tar.gz"
}


package() {

  cp -dpr --no-preserve=ownership "$srcdir/usr" "$pkgdir"
  # Install license
  install -Dm644 "$srcdir/usr/share/doc/steam/copyright" "$pkgdir/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"

}
#vim: set ft=PKGBUILD sw=2 ts=2 et

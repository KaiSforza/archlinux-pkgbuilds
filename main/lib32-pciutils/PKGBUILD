# $Id: PKGBUILD 162716 2012-06-28 20:04:34Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=lib32-pciutils
pkgver=3.1.10
pkgrel=2
pkgdesc="PCI bus configuration space access library (32 bits)"
arch=(x86_64)
license=('GPL2')
groups=('base')
url="http://mj.ucw.cz/sw/pciutils/"
depends=('lib32-glibc' 'pciutils')
makedepends=(gcc-multilib)
source=(#ftp://ftp.kernel.org/pub/software/utils/pciutils/pciutils-${pkgver}.tar.bz2
       ftp://atrey.karlin.mff.cuni.cz/pub/linux/pci/pciutils-${pkgver}.tar.gz)
md5sums=('cdb1e5a95ab7ac67349f10c8f77a6d3c')

build() {
  cd "${srcdir}/pciutils-${pkgver}"
  
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig" 

  make CC="gcc -m32" OPT="${CFLAGS} -fPIC -DPIC" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata LIBDIR=/usr/lib32 MANDIR=/usr/share/man all
  cp lib/libpci.a "${srcdir}/"
  make clean
  make CC="gcc -m32" OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man LIBDIR=/usr/lib32 all
}

package() {
  cd "${srcdir}/pciutils-${pkgver}"
  
  make CC="gcc -m32" SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" LIBDIR=/usr/lib32 install install-lib
  install -m644 "${srcdir}/libpci.a" "${pkgdir}/usr/lib32/"
  
  # this is now supplied by the hwids package
  rm -rf $pkgdir/usr/{sbin,share,include}
}

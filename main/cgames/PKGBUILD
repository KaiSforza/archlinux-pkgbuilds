# Contributor: Joao Cordeiro <jlcordeiro at gmail dot com>

pkgname=cgames
pkgver=2.2
pkgrel=4
pkgdesc="Collection of three ncurses games. csokoban, cmines and cblocks."
arch=('i686' 'x86_64')
url="http://www.muppetlabs.com/~breadbox/software/cgames.html"
license=('GPL2')
depends=(ncurses)
source=(http://www.muppetlabs.com/~breadbox/pub/software/$pkgname-$pkgver.tar.gz
        cgames.patch)
md5sums=('6e260e87728bd67975cafbf09d480a01'
         'd253f16fe56ae9b78f7465ef91e975b6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i "$srcdir/cgames.patch"

  ./configure --prefix=/usr --with-ncurses
  make DESTDIR=$pkgdir
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=$pkgdir datadir=$pkgdir/usr/share/csokoban mandir="$pkgdir/usr/share/man/" bindir="$pkgdir/usr/bin" csokoban-install
  make DESTDIR=$pkgdir datadir=$pkgdir/usr/share/cmines mandir="$pkgdir/usr/share/man/" bindir="$pkgdir/usr/bin" cmines-install
  make DESTDIR=$pkgdir datadir=$pkgdir/usr/share/cblocks mandir="$pkgdir/usr/share/man/" bindir="$pkgdir/usr/bin" cblocks-install
}

# vim:set ts=2 sw=2 et:

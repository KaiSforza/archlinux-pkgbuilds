# Maintainer: William Giokas <1007380@gmail.com>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

pkgname=tp_smapi-mainline
pkgver=0.41
pkgrel=10
pkgdesc="Modules for ThinkPad's SMAPI functionality, mainline kernel"
arch=('i686' 'x86_64')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
depends=('linux-mainline>=3.9' 'linux-mainline<3.10')
makedepends=('linux-mainline-headers>=3.9' 'linux-mainline-headers<3.10')
install="${pkgname}.install"
source=("https://github.com/downloads/evgeni/tp_smapi/tp_smapi-${pkgver}.tar.gz")
md5sums=('63c683415c764568f6bf17c7eabe4752')

build() {
  cd "${srcdir}/tp_smapi-${pkgver}"

  _kernver=${_kernver:-$(uname -r)}
  _extramodules=$(readlink -f "/usr/lib/modules/${_kernver}/extramodules" || true)
  _kernver=$(< "${_extramodules}/version")

  sed -ri -e "s/^(KVER\s*:=).*$/\1 ${_kernver}/" \
	  -e "s|^(KBASE\s*:=).*\$|\1 ${_extramodules%/*}/${_kernver}|" Makefile

  make HDAPS=1
}

package() {
  _kernver=${_kernver:-$(uname -r)}
  _extramodules=$(readlink -f "/usr/lib/modules/${_kernver}/extramodules" || true)
  _kernver=$(< "${_extramodules}/version")

  make -C "${_extramodules%/*}/${_kernver}/build" \
    INSTALL_MOD_PATH="${pkgdir}/${_extramodules%%/lib/*}" \
    M="${srcdir}/tp_smapi-${pkgver}" modules_install

  cd "${pkgdir}/${_extramodules%/*}"
  mv "${_kernver}/extra" "${_extramodules##*/}"
  rmdir "${_kernver}"

  # compress kernel modules
  find "${pkgdir}" -name "*.ko" -exec gzip -9 {} +

  # load module on startup
  echo tp_smapi > "${srcdir}/tp_smapi.conf"
  install -Dm644 "${srcdir}/tp_smapi.conf" "${pkgdir}/usr/lib/modules-load.d/tp_smapi.conf"

  # update kernel version in install file
  sed -ri "s#^(extramodules=).*\$#\1${_extramodules}#" "${startdir}/${pkgname}.install"
}

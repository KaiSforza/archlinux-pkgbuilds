# Maintainer:  Christoph Vigano <mail at cvigano dot de>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Michael Rosset <mrosset@uarch.org>
# Based on [extra]'s git

pkgname=git-git
pkgbase=git
pkgver=1.8.1.rc1.30.g790c83c
pkgrel=1
pkgdesc="The fast distributed version control system - Git version"
arch=('i686' 'x86_64')
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl>=5.14.0' 'openssl' 'pcre')
makedepends=('git' 'asciidoc' 'xmlto' 'docbook-xsl' 'python2' 'emacs')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'perl-mime-tools: git send-email'
            'perl-net-smtp-ssl: git send-email TLS support'
            'perl-authen-sasl: git send-email TLS support'
            'python2: various helper scripts'
            'emacs: emacs interface'
            'subversion: git svn'
            'cvsps: git cvsimport')
provides=('git')
conflicts=('git')
backup=('etc/conf.d/git-daemon.conf')
source=('git-daemon'
        'git-daemon.conf'
        'git://git.kernel.org/pub/scm/git/git.git')

md5sums=('8e2648910fd5dd4f1c41d3c7fa9e9156'
         '2e42bf97779a1c6411d89043334c9e78'
         'SKIP')


pkgver() {
  cd "$srcdir/$pkgbase"
  echo $(git describe | sed 's/-/./g;s/v//g')
}

build() {
  cd "$srcdir/"$pkgbase
	
  export PYTHON_PATH='/usr/bin/python2'
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 all man

  cd contrib/emacs
  make prefix=/usr
}
    
package() {
  export PYTHON_PATH='/usr/bin/python2'

  cd "$srcdir/"$pkgbase
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    INSTALLDIRS=vendor DESTDIR="$pkgdir" install install-man

  # bash completion
  mkdir -p "$pkgdir"/etc/bash_completion.d/
  install -m644 ./contrib/completion/git-completion.bash "$pkgdir"/etc/bash_completion.d/git 
  # more contrib stuff
  cp -a ./contrib "$pkgdir"/usr/share/git/ 
  # scripts are for python 2.x
  sed -i 's|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|' \
    $(find "$pkgdir" -name '*.py') \
    "$pkgdir"/usr/share/git/gitview/gitview

  # emacs interface
  cd contrib/emacs
  make prefix=/usr DESTDIR="$pkgdir" install

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf "$pkgdir"/usr/lib/perl5

  # git daemon script
  install -D -m755 "$srcdir"/git-daemon "$pkgdir"/etc/rc.d/git-daemon
  install -D -m644 "$srcdir"/git-daemon.conf "$pkgdir"/etc/conf.d/git-daemon.conf
}

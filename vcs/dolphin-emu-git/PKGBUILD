# Maintainer: Lightning <sgsdxzy@gmail.com>
# Contributor: Joe Amenta <amentajo@msu.edu>
# Contributor:  Devin Cofer <ranguvar@archlinux.us>
# Contributor: Hans-Kristian Arntzen <maister@archlinux.us>
# Contributor: Zephyr
# Special thanks to Zhou Xin <soul916@hotmail.com> for rewriting this to use cmake

pkgname=dolphin-emu-git
pkgbase=dolphin-emu
pkgver=3.0.873.g43d673b
pkgrel=1
pkgdesc="A GameCube and Wii emulator (Dev tree Git version)"
arch=('i686' 'x86_64')
url="http://www.dolphin-emulator.com/"
license=('GPL2')

makedepends=('git' 'cmake' 'glproto' 'opencl-headers')
depends=('sdl' 'libxxf86vm' 'nvidia-cg-toolkit' 'glew' 'mesa' 'libgl')
provides=("dolphin-emu=${pkgver}-${pkgrel}")
conflicts=('dolphin-emu' 'dolphin-emu-svn-bin' 'dolphin-emu-svn')
optdepends=('wiiuse: for use of real wiimote'
			'portaudio: for mic support'
			'ffmpeg: for dumping frames in AVI format'
			'libao: for aosound backend'
			'openal: for openal backend'
			'pulseaudio: for pulseaudio backend 
			You need to install these optional dependencies and then rebuild dolphin to use them') 
			

source=('dolphin-emu.desktop'
	'Dolphin_Logo.png'
    "git+http://code.google.com/p/$pkgbase/"
)

sha256sums=('1cf1cc034ad03cf068d39fd3bac0103dfb663695966a6846e530d8b8cb363470'
            '4ef79175305df7bb9201931870c4bedc5eda9c94b65311973dfd41c1128784d1'
            'SKIP'
            )

pkgver() {
    cd "$srcdir/$pkgbase"
    git describe --always | sed 's/-/./g'
}


build() {
  cd "${srcdir}/${pkgbase}"

  cmake ../${pkgbase} -DCMAKE_INSTALL_PREFIX=/usr
  make

}
package() {
	cd "${srcdir}/${pkgbase}"

	make DESTDIR=${pkgdir} install

	install -Dm644 "${srcdir}/dolphin-emu.desktop" "${pkgdir}/usr/share/applications/dolphin-emu.desktop"
	install -Dm644 "${srcdir}/Dolphin_Logo.png" "${pkgdir}/usr/share/icons/dolphin-emu.png"
}

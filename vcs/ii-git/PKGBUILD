# Contributor: Anthony Martin <ality@pbrane.org>
# Contributor: Chris Brannon <cmbrannon79 (at) gmail.com>

pkgname=ii-git
_pkgname=ii
epoch=1
pkgver=1.7.r1.g28feb9a
pkgrel=1
pkgdesc="A minimalist FIFO and filesystem-based IRC client"
arch=(i686 x86_64)
url="http://tools.suckless.org/ii/"
license=('MIT')
depends=('glibc')
makedepends=('git')
replaces=('ii-hg')
provides=('ii')
conflicts=('ii')
source=("${_pkgname}::git+http://git.suckless.org/ii#branch=master")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd "$srcdir/$_pkgname"
  # Set a descriptive version for ii-git.
  sed -i "s|^VERSION.*=.*|VERSION = ${pkgver}-${pkgrel}|g" config.mk
}

build() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  # We want ii to be installed with 755, not 775 perms.
  chmod 755 "$pkgdir/usr/bin/ii"
}

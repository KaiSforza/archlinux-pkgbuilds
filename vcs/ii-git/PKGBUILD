# Contributor: Anthony Martin <ality@pbrane.org>
# Contributor: Chris Brannon <cmbrannon79 (at) gmail.com>

pkgname=ii-git
_pkgname=ii
pkgver=1.6.r7.g7a99152
pkgrel=1
pkgdesc="A minimalist FIFO and filesystem-based IRC client"
arch=(i686 x86_64)
url="http://wmii.de/index.php/WMII/ii"
license=('MIT')
depends=('glibc')
makedepends=('git')
replaces=('ii-hg')
provides=('ii')
conflicts=('ii')
source=('config.mk'
	"${_pkgname}::git+http://git.suckless.org/ii#branch=master")
md5sums=('17d6db42d3297691e4a8e98598065f59'
         'SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    git describe --tags --always --long | sed -E 's/v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$_pkgname"
    sed -i "s|%CFLAGS%|$CFLAGS|g" config.mk
    sed -i "s|%LDFLAGS%|$LDFLAGS|g" config.mk

    make
}

package() {
    cd "$srcdir/$_pkgname"
    make DESTDIR="$pkgdir" install

    install -m755 -d  "$pkgdir/usr/share/licenses/ii-git"
    install -m644 LICENSE "$pkgdir/usr/share/licenses/ii-git"
    # make install sets permissions to 775...

    chmod 755 "$pkgdir/usr/local/bin/ii"
    rm -f "$pkgdir/usr/share/doc/ii/LICENSE" # no duplicate files, please
}

# Maintainer: Jonas Haag <jonas@lophus.org>
# Original contributor: Mineo <the_mineo at web dot de>

pkgbase=python-sphinx-hg
pkgname=('python-sphinx-hg' 'python2-sphinx-hg')
pkgver=4061.8ec0b3a74e79
pkgrel=1
pkgdesc="development version of Sphinx, *the* documentation generator"
url="http://sphinx.pocoo.org/latest/"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(
  'python-distribute'
  'python-docutils'
  'python-jinja'
  'python-pygments'
  'python2-distribute'
  'python2-docutils'
  'python2-jinja'
  'python2-pygments'
)
checkdepends=(
  'python-nose'
  'python2-nose'
  'texlive-latexextra'
)

source=("hg+http://bitbucket.org/birkenfeld/sphinx")
_hgname="sphinx"
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_hgname}
  hg identify -ni | awk 'BEGIN{OFS=".";} {print $2,$1}'
}

build() {
  cd ${srcdir}/${_hgname}
  # remove build directory (avoid sed issues)
  [[ -e build ]] && rm -rf build
  # python builds
  python setup.py build --build-lib=build/python
  python2 setup.py build --build-lib=build/python2
  # change python2 interpreter
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
  # change sphinx-binaries name in source code
  find build/python2 -type f -name '*.py' -exec \
    sed -ri 's,(sphinx-(:?build|apidoc|autogen|quickstart)),\12,' {} \;
}

check() {
  #(cd Sphinx-$pkgver/build/python/ && python ../../tests/run.py -d)
  (cd $_hgname/build/python2/ && python2 ../../tests/run.py -d)
}

package_python-sphinx-hg() {
  pkgdesc='Python3 documentation generator'
  depends=('python-jinja' 'python-pygments' 'python-docutils')
  optdepends=('texlive-latexextra: for generation of PDF documentation')
  provides=('python-sphinx')
  conflicts=('python-sphinx')
  
  cd $srcdir/$_hgname
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
}

package_python2-sphinx-hg() {
  pkgdesc='Python2 documentation generator'
  depends=('python2-jinja' 'python2-pygments' 'python2-docutils')
  optdepends=('texlive-latexextra: for generation of PDF documentation')
  provides=('python2-sphinx')
  conflicts=('python2-sphinx')

  cd $srcdir/$_hgname
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
  # fix python3 conflict
  for _f in "$pkgdir"/usr/bin/*; do
      mv -v "$_f" "${_f}2"
  done
}

# Contributor: Jonas Haag <jonas@lophus.org>
# Contributor: Mineo <the_mineo at web dot de>
# Maintainer: William Giokas <1007380@gmail.com>

pkgname=python-sphinx-hg
_pkgname="sphinx"
epoch=1
pkgver=1.2b1.r4164
pkgrel=1
pkgdesc='Python3 documentation generator'
url="http://sphinx.pocoo.org/latest/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('python-jinja' 'python-pygments' 'python-docutils')
optdepends=('texlive-latexextra: for generation of PDF documentation')
makedepends=('python-distribute' 'python-sphinx')
provides=('python-sphinx')
conflicts=('python-sphinx' 'python3-sphinx-hg')

# Not used right now, the test suite fails marvelously with python 3
checkdepends=('python-nose' 'texlive-latexextra')

source=("hg+http://bitbucket.org/birkenfeld/sphinx")
md5sums=('SKIP')

# template start; name=hg-pkgver-tags; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  _revno="$(hg id -n)"
  echo "$(hg log -r "$_revno" --template '{latesttag}' | sed 's/^v//;s/-/./g').r${_revno}"
}
# template end;

build() {
  cd "$srcdir/$_pkgname"
  # python builds
  make build
  # Build docs using sphinx (hence the requirment for python-sphinx)
  sphinx-build -b man doc doc/_build/man
}

package() {
  cd $srcdir/$_pkgname
  python setup.py install --root="$pkgdir" --optimize=1

  # Install the manual pages to man1
  mkdir -p "$pkgdir"/usr/share/man/man1
  for i in "$srcdir/$_pkgname"/doc/_build/man/*.1; do
    install -D -m644 "$i" "$pkgdir"/usr/share/man/man1/"$(basename $i)"
  done
}

# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Dave Reisner <dreisner@archlinux.org> ([core] package)
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org> ([core] package)

pkgname=mkinitcpio-git
pkgver=0.12.0.18.g0ca6cc5
pkgrel=1
pkgdesc="Modular initramfs image creation utility - git checkout"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox' 'kmod' 'util-linux' 'libarchive' 'coreutils'
         'bash' 'findutils' 'grep' 'filesystem' 'gzip' 'systemd-tools')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=("mkinitcpio=$pkgver")
conflicts=('mkinitcpio')
backup=('etc/mkinitcpio.conf')
#source=('mkinitcpio-personal::git+file:///home/wgiokas/git/archlinux/mkinitcpio#branch=fixlib64')
source=('git://projects.archlinux.org/mkinitcpio.git')

pkgver() {
	cd mkinitcpio/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

package() {
	cd mkinitcpio/
	make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP')

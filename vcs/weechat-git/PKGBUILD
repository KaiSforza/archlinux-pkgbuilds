# Maintainer: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Licia Todd <tigrmesh at aol dot com>
# Contributor: Tim Zebulla <amon at faumrahrer dot de>

export CC=clang
DEBUG_CFLAGS="-g"
pkgname=weechat-git
_pkgname=weechat
pkgver=0.4.1.r181.g73f2c2c
pkgrel=2
pkgdesc="Fast, light & extensible IRC client (curses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL3')
depends=('gnutls')
makedepends=('git' 'cmake' 'pkgconfig' 'perl' 'lua' 'tcl' 'aspell' 'clang')
optdepends=('perl' 'lua' 'tcl' 'ruby' 'aspell')
provides=('weechat')
conflicts=('weechat')
options=(!libtool)
source=("git://git.sv.gnu.org/weechat.git"
        '0001-doc-Create-English-man-pages-for-asciidoc-files.patch')
md5sums=('SKIP'
         'f86814dc30f4ebabbe01acacf5fa3804')

prepare() {
  cd "$srcdir/$_pkgname"
  git am "$srcdir/${source[1]}"
}

# template start; name=git-pkgver-r; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}
# template end;

build() {
  cd "$srcdir/$_pkgname"
  cmake "$srcdir/$_pkgname" -DPREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python3 \
        -DPYTHON_LIBRARY=/usr/lib/libpython3.3m.so \
        -DENABLE_RUBY=OFF -DENABLE_DOC=OFF -DENABLE_MAN=ON
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="${pkgdir}/" install
}

# Maintainer: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Licia Todd <tigrmesh at aol dot com>
# Contributor: Tim Zebulla <amon at faumrahrer dot de>

export CC=clang
DEBUG_CFLAGS="-g"
pkgname=weechat-git
_pkgname=weechat
pkgver=0.4.0.r142.gc95f3d8
pkgrel=1
pkgdesc="Fast, light & extensible IRC client (curses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL3')
depends=('gnutls')
makedepends=('git' 'cmake' 'pkgconfig' 'perl' 'python2' 'lua' 'tcl' 'aspell' 'clang')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell')
provides=('weechat')
conflicts=('weechat')
options=(!libtool)
source=("git://git.sv.gnu.org/weechat.git")
md5sums=('SKIP')
_targets=("")

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --always --long | sed -E 's/v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  cmake "$srcdir/$_pkgname" -DPREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DENABLE_RUBY=OFF -DENABLE_DOC=OFF
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="${pkgdir}/" install
}

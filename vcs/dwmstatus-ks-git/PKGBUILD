# Maintainer: William Giokas <1007380@gmail.com>

pkgname=dwmstatus-ks-git
_pkgname=dwmstatus-ks
pkgver=0.5.0.r5.g6be28e7
pkgrel=1
pkgdesc="A simple C status bar, fork by KaiSforza, git version"
url="https://bitbucket.org/KaiSforza/dwmstatus"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
makedepends=('git' 'pacman>4.0.3')
provides=('dwmstatus')
conflicts=('dwmstatus')
source=('dwmstatus-ks::git+git://github.com/KaiSforza/dwmstatus#branch=master'
        'config.h')
md5sums=('SKIP'
         'cb9e8c061bbe89edd8b907af62469312')

# template start; name=git-pkgver-r; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}
# template end;
prepare() {
  cp "$srcdir/config.h" "$srcdir/$_pkgname"
  sed -i "s/VERSION = .*/VERSION = ${pkgver}-${pkgrel}/g" ${srcdir}/${_pkgname}/config.mk
}
build() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr
}
package() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:

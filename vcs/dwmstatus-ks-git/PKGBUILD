# Maintainer: William Giokas <1007380@gmail.com>

pkgname=dwmstatus-ks-git
_pkgname=dwmstatus-ks
pkgver=0.4.0.0.g9c2f70b
pkgrel=2
pkgdesc="A simple C status bar, fork by KaiSforza, git version"
url="https://bitbucket.org/KaiSforza/dwmstatus"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
makedepends=('git' 'pacman>4.0.3')
provides=('dwmstatus')
conflicts=('dwmstatus')
source=('dwmstatus-ks::git+https://bitbucket.org/KaiSforza/dwmstatus.git#branch=working'
        'config.h')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long --always | sed 's/-/./g;s/v//'
}
prepare() {
  cp "$srcdir/config.h" "$srcdir/$_pkgname"
  sed -i "s/VERSION = .*/VERSION = ${pkgver}-${pkgrel}/g" ${srcdir}/${_pkgname}/config.mk
}
build() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr
}
package() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:

# Maintainer: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

_pkgname=youtube-viewer
pkgname=youtube-viewer-git
pkgver=3.0.7.r10.gc18cfde
pkgrel=5
pkgdesc="A command line utility for viewing youtube-videos in MPlayer."
arch=('any')
url="https://github.com/trizen/youtube-viewer"
license=('GPLv3')
makedepends=('git')

conflicts=('youtube-viewer')
provides=('youtube-viewer')

depends=('perl>=5.16.0' 'perl-data-dump' 'perl-libwww')

optdepends=('mplayer: Default media player'
            'perl-xml-fast: faster XML to HASH conversion'
            'perl-term-readkey: to get the terminal width size'
            'perl-term-readline-gnu: for a better STDIN support'
            'perl-lwp-protocol-https: for login and https support'
            'gcap: for retrieving Youtube closed captions. [in AUR]'
            'wget: for downloading videos with wget'
            )

source=('git://github.com/trizen/youtube-viewer.git')
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags --always --long | sed -E 's/v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname/WWW-YoutubeViewer"
  perl Build.PL --destdir="$pkgdir"
  ./Build
}

check() {
  cd "$_pkgname/WWW-YoutubeViewer"
  ./Build test
}

package() {
  cd "$_pkgname/WWW-YoutubeViewer"
  ./Build install --install_path script=/usr/bin --destdir="$pkgdir" --maindir="$pkgdir"
  rm -r "$pkgdir/usr/lib"
}

# Maintainer: William Giokas <1007380@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# PKGBUILD based on extra\deluge's PKGBUILD, adapted for -git version by Berseker

pkgname=deluge-stable
pkgver=20130314
pkgrel=2
pkgdesc="A bittorrent client written with python and pygtk - Git Stable branch Version"
arch=('any')
url="http://deluge-torrent.org/"
license=('GPL3')
depends=('pyxdg' 'libtorrent-rasterbar>=0.15.0' 'twisted' 'python2-pyopenssl'
         'xdg-utils' 'python2-chardet' 'desktop-file-utils' 'hicolor-icon-theme'
         'python2-distribute')
makedepends=( 'intltool' 'pygtk' 'librsvg' 'python2-mako' 'git' 'python2-distribute')
optdepends=('python-notify: libnotify notifications'
            'pygtk: needed for gtk ui'
            'librsvg: needed for gtk ui'
            'python2-mako: needed for web ui')
provides=('deluge')
conflicts=('deluge' 'deluge-svn')
replaces=('deluge-svn')
source=(deluge.tmpfiles.conf
        deluged
        deluge-web
        deluged.service
        deluge-web.service
        deluged.conf)
install=deluge-stable.install
md5sums=('c50385d32a2db0ef3f46b8caadb0e988'
         'cddc352007db5a8bf232c5ee0c59cf4a'
         '37538a1b049b177e9ea1014331e29689'
         '6b831c889f365f58317dc4b78c167a62'
         'b3fff6601a5971bba89fa9a85dcf9ce8'
         '75e591b738cf9f858af2de8665a3b3e7')

_gitroot="git://deluge-torrent.org/deluge.git"
_gitname="deluge"

build() {
  cd "$SRCDEST"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git fetch --progress origin
    msg "The local files are updated."
  else
    git clone --progress --bare $_gitroot $_gitname
  fi
  msg "git sources fetched done or server timeout"

  msg "Creating working copy of %s git repo" "$_gitname"
  rm -rf "${srcdir}/${_gitname}"
  git clone --progress "${SRCDEST}/${_gitname}" "${srcdir}/${_gitname}"
  
  msg "Starting make..."

  cd "${srcdir}/${_gitname}"
  git checkout -b 1.3-stable origin/1.3-stable

  python2 setup.py build
}

package() {
  cd "${srcdir}/$_gitname"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  
  cd "${srcdir}"
  install -Dm644 deluge.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/deluge.conf"
  install -D deluged "$pkgdir/etc/rc.d/deluged"
  install -D deluge-web "$pkgdir/etc/rc.d/deluge-web"
  install -Dm644 deluged.service "$pkgdir/usr/lib/systemd/system/deluged.service"
  install -Dm644 deluge-web.service "$pkgdir/usr/lib/systemd/system/deluge-web.service"
  install -Dm644 deluged.conf "$pkgdir/etc/conf.d/deluged"

  install -d "$pkgdir/srv"
  install -d -m 664 -o 125 -g 125 "$pkgdir/srv/deluge"
}

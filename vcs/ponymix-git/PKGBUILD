# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=ponymix-git
_pkgname=ponymix
pkgver=1.g0.g30cd588
pkgrel=1
pkgdesc="CLI PulseAudio Volume Control"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/ponymix"
license=('MIT')
depends=('pulseaudio')
makedepends=('git')
conflicts=('ponymix' 'pulsemix-git')
provides=('ponymix' 'pulsemix-git')
replaces=('pulsemix-git')

source=("git://github.com/falconindy/ponymix.git")
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$_pkgname
  git describe --always --long | sed -E 's/([^-]*-g)/g\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  make
}

package() {
  make -C "$_pkgname" DESTDIR="$pkgdir" install
}

# vim: et

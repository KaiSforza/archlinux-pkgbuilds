# Maintainer: Daniel Micay <danielmicay@gmail.com>

_pkgname=YouCompleteMe
pkgname=vim-youcompleteme-git
pkgver=0.637.g872b483
pkgrel=1
epoch=2
pkgdesc='A code-completion engine for Vim'
arch=(i686 x86_64)
url='http://valloric.github.com/YouCompleteMe/'
license=(GPL3)
groups=(vim-plugins)
depends=(vim clang)
makedepends=(git cmake)
provides=(vim-youcompleteme)
conflicts=(vim-youcompleteme)
install=vimdoc.install
source=('git://github.com/Valloric/YouCompleteMe.git'
        'git://github.com/davidhalter/jedi.git')
md5sums=('SKIP'
         'SKIP')

# template start; name=git-pkgver-notags; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).g$(git log -1 --format="%h")"
}
# template end;

prepare() {
  cd "$srcdir/$_pkgname"
  sed -i -e "s|https://github.com/davidhalter/jedi.git|../jedi|g" \
    .gitmodules
  git submodule init
  git submodule update
}

build() {
  cd $_pkgname/cpp
  cmake -G "Unix Makefiles" -DUSE_SYSTEM_LIBCLANG=ON
  make ycm_core
}

package() {
  cd $_pkgname
  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -a autoload doc plugin python "$pkgdir/usr/share/vim/vimfiles"
  ln -sf /usr/lib/llvm/libclang.so "$pkgdir/usr/share/vim/vimfiles/python/"
}

# Maintainer:  Matt Monaco <net 0x01b dgbaley27>
# Contributor: Matej Lach <matej.lach@gmail.com>
# Contributor: cantabile <cantabile dot desu at gmail dot com>
# Contributor: extcake < extcake@gmail.com >

pkgname=mplayer2-git
pkgver=298.62ff536
pkgrel=1
pkgdesc="An advanced general-purpose video player."
url="http://www.mplayer2.org"
license=(GPL3)
arch=(x86_64 i686)
install="$pkgname.install"
source=(
    "mplayer2-git::git://git.mplayer2.org/mplayer2-build.git"
)
sha256sums=(
    'SKIP'
)
backup=(
	etc/mplayer/codecs.conf
	etc/mplayer/input.conf
)
depends=(fribidi freetype2 fontconfig desktop-file-utils)
# The dependencies here are *required* to build by the default options. fribidi
# is needed for libass. freetype2 and fontconfig are needed for mplayer itself
# (yes, ./configure --help says they are autodetected, but it lies, they need to
# be explicitly disabled)
makedepends=(git yasm python python-docutils)
conflicts=(mplayer mplayer2)
provides=(mplayer mplayer2)
options=(!emptydirs)

pkgver() {
    cd "$srcdir/$pkgname"
    echo "$(git shortlog | grep -c '^\s\+').$(git describe --always)"
}
build()
{
	cd "$srcdir/$pkgname"
	
	./init --shallow
    sed 's/gmplayer/mplayer/g' -i mplayer/etc/mplayer.desktop
    find -type f -exec sed -e 's/python3/python/' -i {} \;

    (
    pushd mplayer
    ./configure --prefix=/usr --confdir=/etc/mplayer \
            --enable-translation --language=all \
            --enable-runtime-cpudetection \
            --enable-joystick \
            --disable-speex \
            --disable-openal \
            --disable-libdv \
            --disable-musepack
    )

	make
}

package()
{
	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir" install

    install -dm755 $pkgdir/etc/mplayer/
	install -Dm644 mplayer/etc/{codecs,input,example}.conf "$pkgdir/etc/mplayer"
	install -dm755 "$pkgdir/usr/share/mplayer"
	ln -s "../fonts/TTF/DejaVuSans.ttf" "$pkgdir/usr/share/mplayer/subfont.ttf"
	
	install -Dm644 "mplayer/etc/mplayer.desktop" "$pkgdir/usr/share/applications/mplayer.desktop"
	install -Dm644 "mplayer/etc/mplayer.xpm" "$pkgdir/usr/share/pixmaps/mplayer.xpm"
}

# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower-git
_pkgname=cower
pkgver=7.7.g17aee7c
pkgrel=1
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('MIT')
depends=('curl' 'libarchive' 'pacman' 'yajl' )
makedepends=('git' 'perl')
conflicts=('cower')
provides=('cower')

source=("git://github.com/falconindy/cower.git#branch=pacman41")
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$_pkgname
  git describe --long | sed 's/-/./g'
}

build() {
  cd $srcdir/$_pkgname
  make
}

package() {
  make -C "$srcdir/$_pkgname" PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et

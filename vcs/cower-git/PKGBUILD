# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower-git
_pkgname=cower
pkgver=9.r15.ge444ac6
pkgrel=1
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('MIT')
depends=('curl' 'libarchive' 'pacman' 'yajl' )
makedepends=('git' 'perl')
conflicts=('cower')
provides=('cower')

source=("git://github.com/falconindy/cower.git#branch=master"
        "0002-Update-alpm_register_syncdb-for-pacman-git.patch")
md5sums=('SKIP'
         '58594646b76723f7a807159d1cccb095')

prepare() {
  cd "$srcdir/$_pkgname"
  git am "$srcdir/${source[1]}"
}

# template start; name=git-pkgver-r; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}
# template end;

build() {
  cd $srcdir/$_pkgname
  CPPFLAGS+='-DPACMAN_CONFIG=\"/etc/pacman.conf\"' make
}

package() {
  make -C "$srcdir/$_pkgname" PREFIX=/usr DESTDIR="$pkgdir" install
}

# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Daniel Strandberg <esodax!nospam!@gmail.com>

pkgname=zsh-git
_pkgname=zsh
pkgver=5.0.2.10.g44757a6
pkgrel=1
pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
arch=('i686' 'x86_64')
url='http://www.zsh.org/'
license=('custom')
depends=('pcre' 'libcap' 'gdbm')
conflicts=('zsh')
provides=('zsh')
makedepends=('yodl')
install=zsh.install
source=('zprofile'
        'git://zsh.git.sf.net/gitroot/zsh/zsh')
backup=('etc/zsh/zprofile')
md5sums=('24a9335edf77252a7b5f52e079f7aef7'
         'SKIP')


_gitname="$_pkgname"

pkgver() {
    cd "$srcdir/$_gitname"
    echo $(git describe --tags --long | sed 's/zsh-//' | tr - .)
}

build() {
    cd "$srcdir/$_gitname"

    ./Util/preconfig
    
	sed -i 's/init.d/rc.d/g' Doc/Zsh/compsys.yo \
				 Completion/Unix/Type/_services \
				 Completion/Unix/Command/_init_d
				 #Doc/zsh.texi \

	# Set correct keymap path
	sed -i 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' Completion/Unix/Command/_loadkeys

	# Remove unneeded and conflicting completion scripts
	rm -rf Completion/{AIX,BSD,Cygwin,Darwin,Debian,Mandriva,openSUSE,Redhat,Solaris}
	rm -f  Completion/Linux/Command/_{pkgtool,rpmbuild,yast}
	rm -f  Completion/Unix/Command/_osc

	./configure --prefix=/usr \
		--bindir=/bin \
		--enable-etcdir=/etc/zsh \
		--enable-zshenv=/etc/zsh/zshenv \
		--enable-zlogin=/etc/zsh/zlogin \
		--enable-zlogout=/etc/zsh/zlogout \
		--enable-zprofile=/etc/zsh/zprofile \
		--enable-zshrc=/etc/zsh/zshrc \
		--enable-maildir-support \
		--with-term-lib='ncursesw' \
		--enable-multibyte \
		--enable-function-subdirs \
		--enable-fndir=/usr/share/zsh/functions \
		--enable-scriptdir=/usr/share/zsh/scripts \
		--with-tcsetpgrp \
		--enable-pcre \
		--enable-cap \
		--enable-zsh-secure-free
	make
}

check() {
	cd "${srcdir}/$_gitname"
	HOME="${srcdir}" make check
}

package() {
	cd "${srcdir}/$_gitname"
	make DESTDIR="${pkgdir}/" install
	install -D -m644 "${srcdir}/zprofile" "${pkgdir}/etc/zsh/zprofile"
	install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

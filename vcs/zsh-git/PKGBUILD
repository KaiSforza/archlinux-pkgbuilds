# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Daniel Strandberg <esodax!nospam!@gmail.com>

pkgname=zsh-git
_pkgname=zsh
epoch=1
pkgver=5.0.2.r99.g68d0d76
pkgrel=2
pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
arch=('i686' 'x86_64')
url='http://www.zsh.org/'
license=('custom')
depends=('pcre' 'libcap' 'gdbm')
conflicts=('zsh')
provides=("zsh=$pkgver")
makedepends=('yodl')
install=zsh.install
source=('zprofile'
        "$_pkgname::git://git.code.sf.net/p/zsh/code")
backup=('etc/zsh/zprofile')
md5sums=('24a9335edf77252a7b5f52e079f7aef7'
         'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --always --long | sed -E 's/^zsh.//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"

  ./Util/preconfig

	# Set correct keymap path
	sed -i 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' Completion/Unix/Command/_loadkeys

	# Remove unneeded and conflicting completion scripts
	rm -rf Completion/{AIX,BSD,Cygwin,Darwin,Debian,Mandriva,openSUSE,Redhat,Solaris}
	rm -f  Completion/Linux/Command/_{pkgtool,rpmbuild,yast}
	rm -f  Completion/Unix/Command/_osc

	./configure --prefix=/usr \
		--enable-etcdir=/etc/zsh \
		--enable-zshenv=/etc/zsh/zshenv \
		--enable-zlogin=/etc/zsh/zlogin \
		--enable-zlogout=/etc/zsh/zlogout \
		--enable-zprofile=/etc/zsh/zprofile \
		--enable-zshrc=/etc/zsh/zshrc \
		--enable-maildir-support \
		--with-term-lib='ncursesw' \
		--enable-multibyte \
		--enable-function-subdirs \
		--enable-fndir=/usr/share/zsh/functions \
		--enable-scriptdir=/usr/share/zsh/scripts \
		--with-tcsetpgrp \
		--enable-pcre \
		--enable-cap \
		--enable-zsh-secure-free
	make
}

check() {
	cd "${srcdir}/$_pkgname"
	HOME="${srcdir}" make check
}

package() {
	cd "${srcdir}/$_pkgname"
	make DESTDIR="${pkgdir}/" install
	install -D -m644 "${srcdir}/zprofile" "${pkgdir}/etc/zsh/zprofile"
	install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

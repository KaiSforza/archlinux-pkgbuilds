# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=i3status-git
_pkgname=i3status
pkgver=2.6.22.g11757b8
pkgrel=1
pkgdesc="Generates status bar to use with dzen2 or wmii"
arch=('i686' 'x86_64')
url="http://i3wm.org/i3status/"
license=('BSD')
depends=('wireless_tools' 'confuse' 'alsa-lib' 'yajl')
optdepends=("dzen2-svn: As statusbar, dzen2 stable doesn't support -dock yet.")
makedepends=('git' 'pkgconfig' 'asciidoc')
options=('docs')
install=i3status.install
conflicts=('i3status')
provides=('i3status')
groups=('i3-vcs')
source=(git://code.i3wm.org/i3status)
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed 's/-/./g'
}

build() {
  cd "$_pkgname"
  # Remove comment if you want to use i3status with wmii
  # sed -i "s/CFLAGS+=-DDZEN/#CFLAGS+=-DDZEN/g" Makefile
  make
}

package() {
  cd "${_pkgname}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  make clean
}

# vim:set ts=2 sw=2 et:


# Maintainer: William Giokas <1007380@gmail.com>

pkgname=dwm-ks-git
_pkgname=dwm
pkgver=6.0.r90.g5809321
pkgrel=1
pkgdesc="A dynamic window manager for X - config by KaiSforza"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11' 'libxinerama')
optdepends=('proggyfonts: for the default font'
            'dwmstatus-ks: a simple status line')
makedepends=('git' 'clang')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
source=(git+file:///home/wgiokas/git/KaiSforza/dwm#branch=patched)
md5sums=('SKIP')
export CC=clang

# template start; name=git-pkgver-r; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}
# template end;
prepare() {
  cd "$srcdir/$_pkgname"
  sed -i "s/VERSION = .*/VERSION = ${pkgver}-${pkgrel}/g" config.mk
}
build() {
  cd "$srcdir/$_pkgname"
  make
}
package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}

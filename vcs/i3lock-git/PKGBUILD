# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=i3lock-git
pkgbase=i3lock
pkgver=2.4.1.11.g2c9e46d
pkgrel=1
pkgdesc="An improved screenlocker based upon XCB and PAM"
arch=('i686' 'x86_64')
url="http://i3wm.org"
license=('MIT')
depends=('xcb-util-image' 'pam' 'libev' 'cairo' 'libxkbcommon' 'libxkbfile')
provides=('i3lock')
conflicts=('i3lock')
groups=('i3-vcs')
makedepends=('git')
source=(git://code.i3wm.org/i3lock)
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgbase"
  git describe --tags | sed 's/-/./g'
}

build() {
  cd "$pkgbase"
  make
  gzip i3lock.1
}


package() {  
  cd "$pkgbase"

  make DESTDIR="$pkgdir" install
  
  install -Dm644 i3lock.1.gz ${pkgdir}/usr/share/man/man1/i3lock.1.gz
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  make clean
}

# vim:set ts=2 sw=2 et:


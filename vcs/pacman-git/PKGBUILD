# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

export CC=clang
pkgname=(pacman-git pacman-contrib-git)
pkgbase=pacman-git
_pkgname=pacman
pkgver=4.1.0rc1.39.gf89f460
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A library-based package manager with dependency support. git version. Allan's branch."
url="http://www.archlinux.org/pacman/"
license=('GPL')
makedepends=('git' 'asciidoc')
checkdepends=('python2' 'fakechroot')
options=('!libtool' '!strip')
source=('pacman-allan::git+git://projects.archlinux.org/users/allan/pacman.git#branch=working')
#source=('pacman-official::git+git://projects.archlinux.org/pacman.git')
#source=('pacman-local::git+file:///home/wgiokas/git/archlinux/pacman#branch=vodik-2')
_pkgnamedir="${source[0]%%::*}"
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgnamedir"
  echo "$(git describe --long | sed 's#-#.#g;s#^v##')"
}

build() {
  cd "$srcdir/$_pkgnamedir"

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-doc \
    --enable-git-version \
    --enable-debug \
    --disable-doxygen \
    --with-scriptlet-shell=/usr/bin/bash

  time make
  time make -C contrib
}

check() {
  cd "$srcdir"
  make -C "$_pkgnamedir" check
}

package_pacman-git() {
  depends=('glibc>=2.15' 'bash' 'curl>=7.19.4' 'libarchive>=3.1.2' 'gpgme' 'pacman-mirrorlist' 'archlinux-keyring')
  optdepends=('fakeroot: for makepkg usage as normal user')
  backup=(etc/pacman.conf
          etc/makepkg.conf)
  provides=("pacman=$pkgver" 'libalpm.so')
  conflicts=('pacman')
  cd "$srcdir/$_pkgnamedir"

  make DESTDIR="$pkgdir" install

  # set things correctly in the default conf file
  case $CARCH in
    i686)
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686 "
      ;;
    x86_64)
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64 "
      ;;
  esac
  sed -i "$pkgdir/etc/makepkg.conf" \
    -e "s|@CARCH[@]|$CARCH|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
}

package_pacman-contrib-git() {
  provides=("pacman-contrib=$pkgver")
  conflicts=('pacman-contrib')
  depends=('pacman')
  arch=('any')
  # contrib
  cd "$srcdir/$_pkgnamedir"
  make DESTDIR="$pkgdir" -C contrib install

  install -Dm644 "contrib/PKGBUILD.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
  install -dm755 "$pkgdir/usr/share/vim/vimfiles/ftdetect"
  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    >"$pkgdir/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim"

  # install completion files
  rm -r "$pkgdir/etc/bash_completion.d"
  install -Dm644 contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/pacman"
  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done

  install -Dm644 contrib/zsh_completion $pkgdir/usr/share/zsh/site-functions/_pacman
}

# vim: set ts=2 sw=2 et:

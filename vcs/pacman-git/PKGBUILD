# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

pkgname=pacman-git
pkgver=4.0.3_437_g8760843
pkgrel=1
pkgdesc="A library-based package manager with dependency support. git version. Allan's branch."
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'curl' 'gpgme' 'libarchive' 'pacman-mirrorlist')
makedepends=('git' 'asciidoc')
optdepends=('fakeroot: for makepkg usage as normal user')
checkdepends=('python2')
provides=('pacman' 'pacman-contrib' 'libalpm.so')
conflicts=('pacman' 'pacman-contrib')
options=('!libtool' '!strip')
backup=(etc/pacman.conf
        etc/makepkg.conf)
source=('pacman::git+file:///home/wgiokas/git/archlinux/pacman#branch=master-wgiokas')
#source=('pacman::git+git://projects.archlinux.org/users/allan/pacman.git#branch=working')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/pacman"
  #git describe orign-allan/working | sed 's/-/./g'
  echo $(git describe | sed 's#-#_#g;s#v##')
}

build() {
  cd "$srcdir/pacman"
  #git checkout -b makepkg

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-doc \
    --enable-git-version \
    --enable-debug \
    --with-scriptlet-shell=bash

  make
  make -C contrib
}

check() {
  cd "$srcdir/pacman"
  #git checkout makepkg
  cd "$srcdir"
  make -C "pacman" check
}

package() {
  cd "$srcdir/pacman"
  #git checkout makepkg

  make DESTDIR="$pkgdir" install

  # set things correctly in the default conf file
  case $CARCH in
    i686)
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686 "
      ;;
    x86_64)
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64 "
      ;;
  esac
  sed -i "$pkgdir/etc/makepkg.conf" \
    -e "s|@CARCH[@]|$CARCH|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # contrib
  make DESTDIR="$pkgdir" -C contrib install

  install -Dm644 "contrib/PKGBUILD.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
  install -dm755 "$pkgdir/usr/share/vim/vimfiles/ftdetect"
  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    >"$pkgdir/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim"

  # install completion files
  rm -r "$pkgdir/etc/bash_completion.d"
  install -Dm644 contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/pacman"
  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done

  install -Dm644 contrib/zsh_completion $pkgdir/usr/share/zsh/site-functions/_pacman
}

# vim: set ts=2 sw=2 et:

# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: godane <slaxemulator@gmail.com.com>
# Contributor: Andres Perera <aepd87@gmail.com>

export CC=clang
DEBUG_CFLAGS="-g"
pkgname=pacman-git
_pkgname=pacman
pkgver=4.1.1.r53.gb7f8f5d
pkgrel=3
arch=('i686' 'x86_64')
pkgdesc="A library-based package manager with dependency support. git version. Allan's branch."
url="http://www.archlinux.org/pacman/"
license=('GPL')
pkgdesc="A library-based package manager with dependency support. git version. Allan's branch."
depends=('glibc>=2.15'
         'bash'
         'curl>=7.19.4'
         'libarchive>=3.1.2'
         'gpgme'
         'pacman-mirrorlist'
         'archlinux-keyring'
         'openssl'
         ## sodepends: commented until this is implemented fully
         #libarchive.so
         #libc.so
         #libcrypto.so.1.0
         #libcurl.so
         #libgpgme.so
         )
optdepends=('fakeroot: for makepkg usage as normal user')
makedepends=('git'
             'asciidoc'
             )
checkdepends=('python2'
              'fakechroot'
              )
options=('!libtool')
source=('pacman::git+git://projects.archlinux.org/users/allan/pacman.git#branch=working')
sha1sums=('SKIP')
backup=(etc/pacman/pacman.conf
        etc/pacman/makepkg.conf)
provides=("pacman=$pkgver"
          'libalpm.so')
replaces=pacman-contrib
conflicts=('pacman')

# template start; name=git-pkgver-r; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}
# template end;

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/pacman \
    --localstatedir=/var \
    --enable-doc \
    --enable-git-version \
    --enable-debug \
    --disable-doxygen \
    --with-scriptlet-shell=/usr/bin/bash

  time make
  time make -C contrib
}

check() {
  cd "$srcdir"
  make -C "$_pkgname" check
}

package() {
  cd "$srcdir/$_pkgname"

  # main {{{
  make DESTDIR="$pkgdir" install

  # set things correctly in the default conf file
  if [[ $CARCH == 'x86_64' ]]; then mychost="$CARCH-unknown-linux-gnu";
  else                              mychost="$CARCH-pc-linux-gnu"; fi

  myflags="-march=$CARCH"
  sed -i "$pkgdir/etc/pacman/makepkg.conf" \
    -e "s|@CARCH[@]|$CARCH|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
  #}}}

  # contrib {{{
  make DESTDIR="$pkgdir" -C contrib install

  install -Dm644 "contrib/PKGBUILD.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
  install -dm755 "$pkgdir/usr/share/vim/vimfiles/ftdetect"
  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    >"$pkgdir/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim"

  # install completion files
  rm -r "$pkgdir/etc/pacman/bash_completion.d"
  install -Dm644 contrib/bash_completion "$pkgdir/usr/share/bash-completion/completions/pacman"
  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done

  install -Dm644 contrib/zsh_completion $pkgdir/usr/share/zsh/site-functions/_pacman
  # }}}
}

# vim: set ts=2 sw=2 et:

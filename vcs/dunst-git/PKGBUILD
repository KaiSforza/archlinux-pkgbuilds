# Maintainer: Army
# Contributor: William Giokas (KaiSforza) <1007380@gmail.com>
pkgname=dunst-git
pkgver=0.5.0.109.gc2a5b52
pkgrel=1
pkgdesc="A lightweight replacement for the notification-daemons provided by most desktop environments"
arch=('i686' 'x86_64')
url="http://www.knopwob.org/dunst"
license=('MIT')
depends=('dbus' 'libxinerama' 'libxft' 'libxss')
makedepends=('git' 'perl')
optdepends=('libnotify: dunst is able to listen to notifications from libnotify')
provides=('dunst' 'notification-daemon')
conflicts=('dunst')

source=("git+https://github.com/knopwob/dunst.git")
md5sums=('SKIP')
_gitname="dunst"

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --tags --always --long | sed 's/-/./g;s/v//'
}

build() {
  cd "$srcdir/$_gitname"
	msg "use default config.h"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" PREFIX=/usr install
  install -Dm644 "${srcdir}/$_gitname/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

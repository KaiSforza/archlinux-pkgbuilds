# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  Patrick Palka <patrick@parcs.ath.cx>
# Contributor:  Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>
# Development: http://closure.ath.cx/aur-dev

_pkgname=cgit
pkgname=cgit-git
pkgver=0.9.2.r3.g52c926c
epoch=2
pkgrel=1
pkgdesc="A web interface for git written in plain C"
arch=('i686' 'x86_64')
url="http://git.zx2c4.com/cgit/"
license=('GPL')
depends=('openssl')
makedepends=('git' 'zlib' 'curl' 'asciidoc')
install=$_pkgname.install

source=("apache.example.conf"
        "$_pkgname.install"
        "$_pkgname::git://git.zx2c4.com/cgit/")

# template start; name=git-pkgver-r; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}
# template end;

prepare() {
  cd "$srcdir"/"$_pkgname"
  sed -ie 's|url = .*|url = /home/wgiokas/.cache/pacman/src/git|' .gitmodules
  git submodule init
  git submodule update
}
build() {
  cd "$srcdir"/"$_pkgname"
  unset CFLAGS
  unset LDFLAGS
  make
  make doc-man
}

package() {
  cd "$srcdir"/"$_pkgname"
  unset CFLAGS
  unset LDFLAGS
  make CGIT_SCRIPT_PATH=/usr/share/webapps/cgit DESTDIR="$pkgdir" install install-man
  install -vDm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
  install -vd "$pkgdir"/var/cache/"$_pkgname"
  install -vDm0644 "$srcdir"/apache.example.conf "$pkgdir"/etc/webapps/cgit/apache.example.conf
  mkdir -p "$pkgdir"/usr/lib/cgit
  mv "$pkgdir"/usr/share/webapps/cgit/cgit.cgi "$pkgdir"/usr/lib/cgit
  ln -sf ../../../lib/cgit/cgit.cgi "$pkgdir"/usr/share/webapps/cgit/cgit.cgi
}
md5sums=('bd5696bea0654bfcf98f249b1a36737d'
         'aa05c5ed556bf30a4dd32281fd00e0c4'
         'SKIP')

# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl-git
pkgver=0.1.8.gfafa660
pkgver() {
    cd "$srcdir/netctl"
    git describe | tr - .
}
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="http://archlinux.org/netctl/"
license=("GPL")
depends=("coreutils" "iproute2" "openresolv")
makedepends=('asciidoc')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support, or use dhcpcd'
            'dhcpcd: for DHCP support, or use dhclient'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ifenslave: for bond connections'
            'bridge-utils: for bridge connections'
            )
conflicts=("netcfg" "netctl")
provides=("netcfg" "netctl")

source=(git://github.com/joukewitteveen/netctl.git)
md5sums=('SKIP')
arch=(any)

package() {
  cd "$srcdir/netctl"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
}


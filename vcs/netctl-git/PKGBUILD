# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl-git
_pkgname=netctl
epoch=1
pkgver=1.0.r3.g711c464
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="http://archlinux.org/netctl/"
license=("GPL")
depends=("coreutils" "iproute2" "openresolv")
makedepends=('asciidoc')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support, or use dhcpcd'
            'dhcpcd: for DHCP support, or use dhclient'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ifenslave: for bond connections'
            'bridge-utils: for bridge connections'
            )
conflicts=("netcfg" "netctl")
provides=("netcfg" "netctl")

source=(#git://github.com/joukewitteveen/netctl.git
        git://projects.archlinux.org/netctl.git)
md5sums=('SKIP')
arch=(any)

# template start; name=git-pkgver-r; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -E 's/([^-]+-g)/r\1/;s/-/./g;s/^v//g'
}
# template end;

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  #install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}

# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl-git
epoch=1
pkgver=0.8.r21.ge01b395
pkgver() {
    cd "$srcdir/netctl"
    git describe --tags --always --long | sed -E 's/v//;s/([^-]*-g)/r\1/;s/-/./g'
}
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="http://archlinux.org/netctl/"
license=("GPL")
depends=("coreutils" "iproute2" "openresolv")
makedepends=('asciidoc')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support, or use dhcpcd'
            'dhcpcd: for DHCP support, or use dhclient'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ifenslave: for bond connections'
            'bridge-utils: for bridge connections'
            )
conflicts=("netcfg" "netctl")
provides=("netcfg" "netctl")

source=(#git://github.com/joukewitteveen/netctl.git
        git://projects.archlinux.org/netctl.git)
md5sums=('SKIP')
arch=(any)

package() {
  cd "$srcdir/netctl"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  #install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
}

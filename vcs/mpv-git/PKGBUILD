# Maintainer: RudolfPolzer <divVerent@xonotic.org>
# Based on the mplayer2-lite-git PKGBUILD

export CC=clang
pkgname=mpv-git
_pkgname="mpv"
pkgver=0.35056.gb718f32
pkgrel=1
pkgdesc="A movie player and encoder for linux (uses dynamically linked ffmpeg; git version)"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/mpv-player/mpv"
depends=('libcaca' 'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libmng' 'libbluray' 'libcdio-paranoia' 'libxinerama' 'libxss' 'desktop-file-utils' 'libvdpau' 'libass' 'libquvi' 'mpg123')
makedepends=('git' 'mesa' 'python-docutils')
provides=('mpv')
conflicts=('mpv')
options=(!emptydirs)
install=mpv.install
source=('git://github.com/mpv-player/mpv#branch=master'
        'mpv.desktop')
md5sums=('SKIP'
        'afa04e3bdb3ef16b78804760c63a3d9a')

# template start; name=git-pkgver-notags; version=1.0;
pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).g$(git log -1 --format="%h")"
}
# template end;

build() {
  # Custom CFLAGS break the mpv build
  unset CFLAGS
  unset LDFLAGS
  cd "$srcdir/$_pkgname"

  ./configure --prefix=/usr \
      --disable-smb \
      --disable-lirc \
      --disable-lircc \
      --disable-joystick \
      --disable-radio \
      --disable-radio-capture \
      --disable-radio-v4l2 \
      --disable-winsock2_h \
      --disable-macosx-bundle \
      --disable-rpath \
      --disable-vstream \
      --disable-libbs2b \
      --disable-direct3d \
      --disable-corevideo \
      --disable-cocoa \
      --disable-coreaudio \
      --disable-sdl2

  make
}

package() {
  cd "${srcdir}/${_pkgname}"

  make -j1 DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/doc/mpv/examples

  # some extra useful stuff "make install" doesn't install
  install -m644 ${srcdir}/${_pkgname}/etc/{input,example,encoding-example-profiles}.conf "${pkgdir}/usr/share/doc/mpv/examples"
  install -m644 ${srcdir}/${_pkgname}/DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
  install -m755 ${srcdir}/${_pkgname}/TOOLS/mpv_identify.sh "${pkgdir}/usr/bin"
  install -Dm644 "${srcdir}/${_pkgname}/etc/mplayer.xpm" "${pkgdir}/usr/share/pixmaps/mpv.xpm"

  # file association
  install -Dm644 "${srcdir}/mpv.desktop" "${pkgdir}/usr/share/applications/mpv.desktop"

  # Fix fontconfig bug
  install -Dm644 "${srcdir}/${_pkgname}/sub/osd_font.otf" "${pkgdir}/usr/share/fonts/Type1/osd_font.otf"
}

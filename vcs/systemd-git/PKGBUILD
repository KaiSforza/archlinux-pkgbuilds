# Maintainer: Dave Reisner <dreisner@archlinux.org>

#export CC=clang
pkgname=systemd-git
_pkgname=systemd
pkgver=200.36.g11c4c24
pkgrel=1
pkgdesc="system and service manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('acl' 'bash' 'dbus-core' 'glib2' 'kbd' 'kmod' 'libcap' 'libgcrypt' 'pam' 'util-linux' 'xz')
makedepends=('docbook-xsl' 'git' 'gobject-introspection' 'gperf'
             'gtk-doc' 'intltool' 'libxslt' 'python2')
optdepends=('cryptsetup: required for encrypted block devices'
            'python2: systemd library bindings'
            'quota-tools: kernel-level quota management')
provides=('systemd' 'libsystemd' 'systemd-sysvcompat' 'systemd-tools' 'udev=999' 'nss-myhostname')
conflicts=('systemd' 'libsystemd' 'systemd-sysvcompat' 'systemd-tools' 'sysvinit' 'initscripts' 'udev' 'nss-myhostname')
replaces=('nss-myhostname')
groups=('systemd')
options=('!libtool')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/systemd/bootchart.conf
        etc/udev/udev.conf)
install=systemd.install
#source=('systemd::git://anongit.freedesktop.org/systemd/systemd.git'
#source=("systemd-tags::git+file:///home/wgiokas/git/KaiSforza/systemd#commit=c20d829"
# Bad commit: 168f22
source=("systemd-personal::git+file:///home/wgiokas/git/KaiSforza/systemd#branch=master"
        'initcpio-hook-udev'
        'initcpio-install-udev'
        'initcpio-install-timestamp')
md5sums=('SKIP'
         'e99e9189aa2f6084ac28b8ddf605aeb8'
         'fb37e34ea006c79be1c54cbb0f803414'
         'df69615503ad293c9ddf9d8b7755282d')
_gitname="${source[0]%%::*}"

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --long --always | sed 's#-#.#g;s#v##')
}

#prepare() {
#  cd "$srcdir/$_gitname"
#
#  git apply -v "$srcdir/use-split-usr-path.patch"
#
#}
build(){
  cd "$srcdir/$_gitname"
  ./autogen.sh
  ./configure \
      PYTHON=python2 \
      PYTHON_CONFIG=/usr/bin/python2-config \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --disable-gtk-doc \
      --disable-audit \
      --disable-ima \
      --disable-microhttpd \
      --disable-selinux \
      --with-sysvinit-path= \
      --with-sysvrcnd-path= 

  time make
}

check() {
  cd "$srcdir/$_gitname"
  make check
}

package() {
  make -C "$_gitname" DESTDIR="$pkgdir" install

  printf "d /run/console 0755 root root\n" >"$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # compat symlinks
  install -dm755 "$pkgdir"/sbin
  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s '/usr/bin/systemctl' "$pkgdir/sbin/$tool"
  done
  ln -s '../usr/lib/systemd/systemd' "$pkgdir/sbin/init"

  # fix .so links in manpage stubs
  find "$pkgdir/usr/share/man" -type f -name '*.[[:digit:]]' \
      -exec sed -ri '1s|^\.so (.*)\.([0-9]+)|.so man\2/\1.\2|' {} +

  # move bash-completion and symlink for loginctl
  #install -Dm644 "$pkgdir/etc/bash_completion.d/systemd-bash-completion.sh" \
  #  "$pkgdir/usr/share/bash-completion/completions/systemctl"
  #for ctl in {login,journal,timedate,locale,hostname,systemd-coredump}ctl udevadm; do
  #  ln -s systemctl "$pkgdir/usr/share/bash-completion/completions/$ctl"
  #done
  rm -rf "$pkgdir/etc/bash_completion.d"
  install -Dm644 "$srcdir/${_gitname}/shell-completion/${_pkgname}-zsh-completion.zsh" \
      "$pkgdir/usr/share/zsh/site-functions/_${_pkgname}"

  # don't write units to /etc by default
  rm "$pkgdir/etc/systemd/system/getty.target.wants/getty@tty1.service"
  rmdir "$pkgdir/etc/systemd/system/getty.target.wants"

  # get rid of RPM macros
  rm -r "$pkgdir/etc/rpm"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules

  # add mkinitcpio hooks
  install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
  install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"
  install -Dm644 "$srcdir/initcpio-install-timestamp" "$pkgdir/usr/lib/initcpio/install/timestamp"
}
